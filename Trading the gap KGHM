GraphXSpace = 8;
RequestTimedRefresh( 1, False );
SetBarsRequired( sbrAll, sbrAll );
//------------------------------------------
symbol = "KGH";
symbolzlecenia="FKGHU24";
numerRachunku = "00-22-141576";
wielkoscPozycji = 3; //2 sztuka
interwalSystemu = 60 * 60;//15minut
//------------------------------------------
 
chartID = GetChartID();
svBuy = "Buy" + symbolzlecenia + chartID;
svSell = "Sell" + symbolzlecenia + chartID;
svPrevTime =  "PrevTime" + symbolzlecenia + chartID;
 
function isnewBar()
{
    local prevTimeNum;
    local lastTimeNum;
    prevTimeNum = StaticVarGet( svPrevTime );
    lastTimeNum = LastValue( DateTime() );
    StaticVarSet( svPrevTime, lastTimeNum );
    return prevTimeNum != lastTimeNum;
}
 
 
Guibutton( "NOL NADZORCA START", 001, Status( "pxchartwidth" ) / 2, 10, 210, 40, 1 );
GuiSetColors( 001, 001, 2, colorYellow, colorBlue, colorYellow );
id = GuiGetEvent( 0, 0 );
event = GuiGetEvent( 0, 1 );
if( id == 001 && event == 1 )
{
    _TRACE( "Start NOL Nadzorca" );
    StaticVarSet( svPrevTime, 0 );
    StaticVarSet( "outnafix" + chartid, 0 );
    nolInit();
    nolSetTrading( symbolzlecenia, 1 );
}
 
 
//---------------- system --------------------
prowizja= 3;//= 0.425 + 0.58; // prowizja w pkt. Prowizja 8.50 zł czyli 0.425 pkt, 0.58 pkt średni poślizg na transakcje czyli 11.60 zł dodatkowo
SetOption( "CommissionAmount", prowizja ); // ustawienie prowizji
SetOption( "initialequity", 100000 ); //kapital poczatkowy 18000
SetOption( "futuresmode", True ); // tryb pochodne (futures)
SetOption( "AllowSameBarExit" , False ); //nie pozwala na wyjscia na tej samej swiecy co wejscie
SetOption( "AllowPositionShrinking", False );
SetOption( "ActivateStopsImmediately", False ); //stopy sa sprawdzane od razu po wejsciu w pozycje (reakcja juz na slupku z sygnalem kupna)
SetOption( "minshares", 1 ); //minimalna ilosc kontraktów
SetOption( "ReverseSignalForcesExit", true );
SetOption( "ExtraColumnsLocation", 1 );//polozenie kolumn parametrów optymalizacji na poczatek
SetTradeDelays(0,0,0,0);
TickSize = 0.01;
PointValue = 100;
RoundLotSize = 1;
MarginDeposit = 1;
SetPositionSize( 3, spsShares );
/*///////////////////////////////////////////////////////////
System "Trading the Gap KGHM" interwał 15 minut
Copyright:PIOTR STYPUŁKOWSKI argentarius@wp.pl  tel. 603474789
//////////////////////////////////////////////////////////////*/
SetChartOptions( 0, chartShowArrows | chartShowDates );
_N( Title = StrFormat( "{{NAME}} - {{INTERVAL}} {{DATE}} Open %g, Hi %g, Lo %g, Close %g (%.1f%%) {{VALUES}}", O, H, L, C, SelectedValue( ROC( C, 1 ) ) ) );
Plot( Close, "", colorBlack, styleCandle );

///////////////////////////////////////////////////////
gora=TimeFrameGetPrice("O",inDaily,0);
zamkn=TimeFrameGetPrice("C",inDaily,-1);
zmup=3;
zmdown=3.3;
deltaup=(1-zamkn/gora)*100<zmup;
deltadown=(1-gora/zamkn)*100<zmdown; 
 
//////////////////////////////////////////////////////////////////////////////////////////
 
Buy= Hour()==09 AND O >TimeFrameGetPrice("H",inDaily,-1) AND deltaup; 
Sell=C<Ref(L,-2) OR   Hour()==17; 
Short= Hour()==09 AND O <TimeFrameGetPrice("L",inDaily,-1) AND deltadown ; 
Cover=C>Ref(H,-2) OR  Hour()==17; 
//Buy = Ref( Buy, -1 );
//Sell = Ref( Sell, -1 ) OR Hour()==17;// AND Minute()==0045;
//Short = Ref( Short, -1 );
//Cover = Ref( Cover, -1 ) OR Hour()==17;// AND Minute()==0045;
BuyPrice = open;
SellPrice = Close;
ShortPrice = open;
CoverPrice = Close;
eq = Equity( 1, 0 );
mamLong = Flip( Buy, Sell OR short );
mamShort = Flip( Short, Cover OR buy );
Plot( mamLong, "", colorGreen, styleHistogram + styleOwnScale, 0, 20 );
Plot( mamShort, "", colorred, styleHistogram + styleOwnScale, 0, 40 );
Plot( eq, "eq", colorBlue, styleLine + styleOwnScale );
PlotShapes( Buy*shapeUpTriangle, colorBlue, 0, L, -30, 0 );
PlotShapes( sell*shapeDownTriangle, colorgold, 0, H, -30, 0 );
PlotShapes( Short*shapeDownTriangle, colorBlack, 0, H, -30, 0 );
PlotShapes( Cover*shapeUpTriangle, colorgold, 0, L, -30, 0 );
 
 
if( Interval( 0 ) != interwalSystemu  OR Name() != symbol )
{
    GfxSelectFont( "Tahoma", 15 );
    GfxSetTextColor( colorRed );
    GfxSetBkColor( ColorRGB( 10, 20, 30 ) );
    GfxTextOut( "Interwał wykresu lub symbol nie jest zgodny z deklaracją!!!", Status( "pxchartwidth" ) / 2, 100 );
    Buy = Sell = Short = Cover = 0;
}
if( nolMode( symbolzlecenia ) != 2 )
{
    GfxSelectFont( "Tahoma", 15 );
    GfxSetTextColor( colorRed );
    GfxSetBkColor( ColorRGB( 10, 20, 30 ) );
    if( nolMode( symbolzlecenia ) != 2 )
    {
        GfxTextOut( "Nie włączono autotradingu w NOL Nadzorca", Status( "pxchartwidth" ) / 2, 130 );
        Buy = Sell = Short = Cover = 0;
    }
    else
        GfxTextOut( "Włączono atotradingu w NOL Nadzorca", Status( "pxchartwidth" ) / 2, 80 );
}
if( nolConnection( 0 ) != 36 and nolConnection( 0 ) != 4 and nolConnection( 0 ) != 3 )
{
    GfxSelectFont( "Tahoma", 15 );
    GfxSetTextColor( colorRed );
    GfxSetBkColor( ColorRGB( 10, 20, 30 ) );
    GfxTextOut( "Sprawdź połącznie NOL Nadzorcy lub Bossa NOL!", Status( "pxchartwidth" ) / 2, 130 );
}
 
newBar = isnewBar();
 
if( newbar AND LastValue( TimeNum() < 170000 ) )
{
    if( LastValue( sell AND NOT Short ) )
    {
        //OUT z Long
       _TRACE( "OUT z Long" );
        idSell = nolSell( symbolzlecenia, numerRachunku, wielkoscPozycji , 0, 0 );
    }
    else
        if( LastValue( cover AND NOT buy ) )
        {
            //OUT z Short
            _TRACE( "OUT z Short" );
            idBuy = nolBuy( symbolzlecenia, numerRachunku, wielkoscPozycji , 0, 0);
        }
        else
            if( LastValue( buy ) AND NOT LastValue( Ref( mamShort, -1 ) ) )
            {
                // Long po OUT
                _TRACE( "Long po OUT" );
                idBuy = nolBuy( symbolzlecenia, numerRachunku, wielkoscPozycji , 0,0 );
            }
            else
                if( LastValue( short )AND NOT LastValue( Ref( mamlong, -1 ) ) )
                {
                    //Short po OUT
                    _TRACE( "Short po OUT" );
                    idSell = nolSell( symbolzlecenia, numerRachunku, wielkoscPozycji , 0, 0 );
                }
                else
                    if( LastValue( short )AND LastValue( Ref( mamlong, -1 ) ) )
                    {
                        //Odwrocenie z Long na Short
                        _TRACE( "Odwrocenie z Long na Short" );
                        idSell = nolSell( symbolzlecenia, numerRachunku, 2 * wielkoscPozycji , 0, 0 );
                    }
                    else
                        if( LastValue( buy ) AND LastValue( Ref( mamShort, -1 ) ) )
                        {
                            //Odwrocenie z Short na Long
                            _TRACE( "Odwrocenie z Short na Long" );
                            idBuy = nolBuy( symbolzlecenia, numerRachunku, 2 * wielkoscPozycji , 0, 0 );
                        }
}
//odkomentuj jesli grasz na koncie kontraktowym, a nie intradayowym, na którym Bossa sama zamyka pozycje na koniec dnia
 
else
    if( Now( 4 ) > 165000  AND Now( 4 ) < 165500  /*AND nolSessionStat( symbolzlecenia ) == 6*/ AND Nz( StaticVarGet( "outnafix" + chartid, 0 ) ) )
    {
  //out na close
        if( LastValue( mamLong ) )
        {
//zamykam Long
            _TRACE( "Zamykam Long" );
            StaticVarSet( "outnafix" + chartid, 1 );
            idSell = nolSell( symbolzlecenia, numerRachunku, wielkoscPozycji , 0, 0 );
        }
        else
            if( LastValue( mamshort ) )
            {
//zamykam Short
                _TRACE( "Zamykam Short" );
                StaticVarSet( "outnafix" + chartid, 1 );
                idBuy = nolBuy( symbolzlecenia, numerRachunku, wielkoscPozycji , 0, 0 );
            }
    }
////////////////////////////////////////////////*/
 
 
